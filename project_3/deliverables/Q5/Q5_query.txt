PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX ds: <https://data.medicare.gov/d/nrth-mfg3#>

SELECT ?stateName (ROUND(?efficient_count * 100 / ?total) AS ?percent) ?efficient_count ?total
WHERE {
  {
    SELECT ?stateName (COUNT(?efficient) AS ?efficient_count)
    WHERE {
      ?efficient ds:hasHospitalAverageMedicareSpending ?hSpending.
      ?efficient ds:hasScore ?hScore.
      ?efficient ds:hasState ?state.
      ?state ds:hasStateName ?stateName.
      ?state ds:hasStateAverageMedicareSpending ?sSpending.
      
  	  # This is to find out the state average score 
      {
        SELECT (AVG(?innerScore) AS ?stateAvgScore) 
        WHERE {
            ?hospital ds:hasState ?state.
          	?state ds:hasStateName ?stateName.
            ?hospital ds:hasScore ?innerScore.
            FILTER(?innerScore != 0 && ?innerScore != -1) # ignore score == 0 or score == -1
         }
  	  }
      # Filter out: hospital with spending == -1 or 0, then run comparison
      FILTER(?hSpending != 0 && ?hSpending != -1 && ?hSpending < ?sSpending && ?hScore > ?stateAvgScore)
    }
      GROUP BY ?stateName
    }
  # This block is to find the total hospital in each state for percentage calculation
  {
    SELECT ?stateName (COUNT(?hospital) AS ?total)
    WHERE {
        ?hospital ds:hasState ?state.
      	?state ds:hasStateName ?stateName.
    }
    GROUP BY (?stateName)
  }
}
ORDER BY DESC(?percent)
